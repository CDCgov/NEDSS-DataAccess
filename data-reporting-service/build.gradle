buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'org.liquibase.gradle' version '2.2.2'
}

//apply plugin: 'org.owasp.dependencycheck'

group = 'gov.cdc.eltdatapipeline'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

tasks.withType(Jar).configureEach { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Liquibase
    liquibaseRuntime 'org.liquibase:liquibase-core:4.27.0'
    liquibaseRuntime 'info.picocli:picocli:4.7.5'
    liquibaseRuntime 'com.microsoft.sqlserver:mssql-jdbc:12.6.1.jre11'
}

tasks.named('test') {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Main-Class': 'gov.cdc.etldatapipeline.EtlDataPipelineApplication'
        )
    }
    zip64 true
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // Gather all runtime dependencies
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

apply plugin: "org.liquibase.gradle"

liquibase {
    activities {
        odse_main {
            changelogFile './src/main/resources/db/changelog/db.odse.changelog-master.yaml'
            url "jdbc:sqlserver://${System.env.DB_URL}:1433;databaseName=${System.env.DB_ODSE};encrypt=true;trustServerCertificate=true"
            username "${System.env.DB_USERNAME}"
            password "${System.env.DB_PASSWORD}"
        }
        rdb_main {
            changelogFile './src/main/resources/db/changelog/db.rdb.changelog-master.yaml'
            url "jdbc:sqlserver://${System.env.DB_URL}:1433;databaseName=${System.env.DB_RDB};encrypt=true;trustServerCertificate=true"
            username "${System.env.DB_USERNAME}"
            password "${System.env.DB_PASSWORD}"
        }
    }
}